---
title: Сортируем ДанныеФормыДерево на клиенте
date: 2019-06-18 01:22:00 +/-TTTT
description: Иногда так хочется, но нечем...
media_subpath: /assets/posts/2019-06-18-sort-FormDataTree-on-client/
categories: [Заметки]
tags: [1С, Заметки, Желтый Чайник 1С, Метод]
image:
  path: cover.png
---

У `ДанныеФормыКоллекция` для сортировки есть специальный метод `Сортировать()`. Он доступен на клиенте и, хоть и делает вызов сервера, но, по понятным причинам, срабатывает крайне быстро.

А вот `ДанныеФормыДерево` платформа обделила такой возможностью. Вместо этого программисты обычно переходят «на сервер», преобразуют `ДанныеФормыДерево` в полноценное `ДеревоЗначений`, сортируют и преобразуют обратно.

```bsl
ДеревоЗначений    = ДанныеФормыВЗначение(ДанныеФормыДерево, Тип("ДеревоЗначений")); 
ДеревоЗначений.Строки.Сортировать("КолонкиСортировки", Истина); 
ЗначениеВДанныеФормы(ДанныеФормыДерево, ДеревоЗначений);
```

А ведь существуют ситуации, когда перебрасывать данные всей формы «на сервер» значительно затратнее, чем, оставаясь «на клиенте», отсортировать десяток строчек.

Для реализации [Менеджер открытых форм]({% link _posts/projects/2017-04-13-ManagerOpenForms.md %}) опробовал разные алгоритмы сортировки ДанныеФормыДерево, доступные на клиенте. В результате, остановился на этом, что помогло ускорить открытие формы обработки в 2,5 раза.

Выкладываю процедуру. Она поможет в тех случаях, когда вам явно по одной колонке отсортировать коллекцию. Если будет ясно, что метод нуждается в расширении возможностей, то процедура со временем доработается и статья обновится.

_P.S.: Есть альтернативные варианты процедуры? Выкладывайте в комментариях. Если наберётся достаточное количество интересных методов, то после можем провести нагрузочное тестирование, выложить результаты сравнения и выбрать процедуру-победитель._

```bsl
&НаКлиентеНаСервереБезКонтекста
Процедура СортироватьДанныеФормыДерево(КоллекцияСортировки, КолонкаСортировки, ВключатьПодчиненные = Ложь)

  КоллекцияСтрок  = КоллекцияСортировки.ПолучитьЭлементы();

  СортироватьДанныеФормыКоллекция(КоллекцияСтрок, КолонкаСортировки);

  Если ВключатьПодчиненные Тогда
    Для Каждого ТекущаяСтрока Из КоллекцияСтрок Цикл
      СортироватьДанныеФормыДерево(ТекущаяСтрока, КолонкаСортировки, ВключатьПодчиненные);
    КонецЦикла;
  КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СортироватьДанныеФормыКоллекция(КоллекцияСтрок, КолонкаСортировки)

  ПараметрыЗначений     = Новый Соответствие;
  СортированныеЗначения = Новый СписокЗначений;

  Для Каждого ТекущаяСтрока Из КоллекцияСтрок Цикл

    ТекущееЗначение = ТекущаяСтрока[КолонкаСортировки];

    ПараметрыЗначения = ПараметрыЗначений.Получить(ТекущееЗначение);
    Если ПараметрыЗначения = Неопределено Тогда
      ПараметрыЗначения = Новый Массив;
      ПараметрыЗначений.Вставить(ТекущееЗначение, ПараметрыЗначения);
      СортированныеЗначения.Добавить(ТекущееЗначение);
    КонецЕсли;

    ПараметрыЗначения.Добавить(ТекущаяСтрока);

  КонецЦикла;

  СортированныеЗначения.СортироватьПоЗначению(НаправлениеСортировки.Возр);


  НовыйИндекс = 0;
  Для Каждого ТекущееЗначение Из СортированныеЗначения Цикл

    МассивСтрок = ПараметрыЗначений.Получить(ТекущееЗначение.Значение);
    Для Каждого ТекущаяСтрока Из МассивСтрок Цикл

      ТекущийИндекс = КоллекцияСтрок.Индекс(ТекущаяСтрока);
      ШагСдвига     = НовыйИндекс - ТекущийИндекс;
      Если НЕ ШагСдвига = 0 Тогда
        КоллекцияСтрок.Сдвинуть(ТекущийИндекс, ШагСдвига);
      КонецЕсли;

      НовыйИндекс   = НовыйИндекс + 1;

    КонецЦикла;

  КонецЦикла;

КонецПроцедуры
```
