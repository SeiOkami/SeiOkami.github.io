---
title: Как ограничить поля отбора в динамическом списке и ничего не сломать
date: 2020-05-06 10:00:00 +/-TTTT
description: С чем разработчик может столкнуться при попытке установки ограничений в динамическом списке.
media_subpath: /assets/posts/articles/2020-05-06-limit-selection-fields-in-dynamic-list/
categories: [Статьи]
tags: [1С, Статьи, Желтый Чайник 1С]
image:
  path: cover.jpeg
  in_post: false
links:
  top: true
  bottom: true
  values:
  - name: Статья на Инфостарт
    url: https://infostart.ru/1c/articles/1226445/?ref=1159
  - name: Пост в Telegram
    url: https://t.me/JuniorOneS/65
---

Динамические списки - это один из базовых объектов для разработок на базе 1С. 

Однако, долгое время разработчики не имели возможность запрещать пользователям делать отборы по конкретным полям и их реквизитам. Такое бывает необходимо, чтобы не дать возможность "положить" списки со сложными запросами или с большим количеством данных.

И вот с версии 8.3.10 в платформе появился метод `УстановитьОграниченияИспользованияВОтборе()`

В справке нас интересует этот пункт:
> Запрещает использование указанных полей и всех их дочерних полей в настройках отбора.
> Поля, на которые установлено ограничение, не входят в коллекцию доступных полей.
> **После вызова метода список полей с ограничениями замещается указанным. Вызов метода с пустым списком полей отменяет ранее установленные ограничения**

Это означает, что метод можно вызывать повторно, настраивая доступность полей в зависимости от каких-то событий. Но нужно понимать нюансы работы.

Обычно метод применяют в одном событии формы - `ПриСозданииНаСервере()`. Когда платформа ещё не приступила к непосредственному открытию формы и всячески к этому готовится. В такие моменты разработчик отбирает у пользователей возможность фильтрации по каким-то полям. А что если менять состав полей несколько раз?

Приведём такой пример. Есть динамический список с документами. Необходимо запретить пользователям делать фильтры по полям, если в шапке формы пустой параметр `Организация`. Возьмём для нашего эксперимента форму журнала документов продажи из демо базы ERP 2.4:

![Скрин](01.png)

Для этого добавим форму в расширение:

![Скрин](02.png)

Подключимся к обработчику изменения организации:

![Скрин](03.png)

И вставим такой простой код:

```bsl
&НаСервере
&После("УстановитьОтборПоОрганизации")
Процедура ЖурналДокументовПродажи_УстановитьОтборПоОрганизации()
	
	ЗапрещенныеВОтбореПоля = Новый Массив;
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		ЗапрещенныеВОтбореПоля.Добавить("Подразделение");
	КонецЕсли;
	
	СписокДокументыПродажи.УстановитьОграниченияИспользованияВОтборе(ЗапрещенныеВОтбореПоля);
	
	ОтключитьНедоступныеОтборы(СписокДокументыПродажи.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы);
	
КонецПроцедуры
```

Обновим базу и откроем форму в режиме предприятия. Сразу видим, что в контекстном меню по полю "Подразделение" нет возможности поиска:

![Скрин](04.png)

Расширенный поиск не позволяет выбирать подразделение:

![Скрин](05.png)

Так же отбор недоступен и в форме настройки списка:

![Скрин](06.png)

А теперь заполним организацию:

![Скрин](07.png)

И появилась возможность фильтровать в расширенном поиске:

![Скрин](08.png)

Ну и настройка динамического списка заработала:

![Скрин](09.png)

А значит теперь мы можем наложить отбор на подразделение. Давайте так и сделаем.

Сейчас в списке доступны три строки с двумя подразделениями:

![Скрин](10.png)

Установим отбор по подразделению "Дирекция" при помощи `Ctrl+Alt+F`

![Скрин](11.png)

Всё хорошо. Но что будет, если мы сейчас очистим организацию в шапке формы, тем самым запретив отбирать по подразделению?

![Скрин](12.png)

Странно, да? Отбор отключился. Но надпись осталась. Да, её не трудно закрыть крестиком вручную, но пользователю будет непонятно, почему показан отбор, который не работает.

Такое странное поведение платформы можно наблюдать вплоть до 8.3.16. К сожалению, никаким образом на более ранних версиях платформы обойти этот баг исправить не удаётся. Приходится просто отключать отображение состояния просмотра. Если вдруг найдёте другой способ - напишите в комментариях.

Ну хорошо, допустим мы свыклись с тем, что надпись не обновляется и отключили её. Но что с отбором через "Настроить список" ?

Для начала снова выберем организацию в шапке формы и установим отбор по подразделению:

![Скрин](13.png)

Результат такой:

![Скрин](14.png)

Хорошо, а теперь очистим организацию в шапке:

![Скрин](15.png)

Мы установили ограничение на отбор, но существующий при этом не сбросился. А в настройке списка наш отбор помечен красным и более недоступен:

![Скрин](16.png)

Как же так? Понять это поможет другая статья, в которой рассказывается принцип того, как СКД (на которой и базируются динамические списки) работает с ограничениями полей: [Ограничения полей, или как обмануть СКД]({% link _posts/articles/2020-04-05-field-constraints-how-fool-skd.md %})

Прочитав её, станет понятно, что ограничения полей в отборах полноценно влияют только на пользовательскую доступность полей, но не на логику выборки данных. То есть, пользовательские настройки обходят ограничения полей.

Интересно, что решение, которое было предложено в статье, в данной ситуации не работает. Если использовать `КомпоновщикНастроек.Восстановить()`, то пользовательские отборы всё равно остаются на месте.

Попробуем же программно отключить использование отборов, если их полей нет в коллекции ДоступныеПоляОтбора.

```bsl
&НаСервере
&После("УстановитьОтборПоОрганизации")
Процедура ЖурналДокументовПродажи_УстановитьОтборПоОрганизации()
	
	ЗапрещенныеВОтбореПоля = Новый Массив;
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		ЗапрещенныеВОтбореПоля.Добавить("Подразделение");
	КонецЕсли;
	
	СписокДокументыПродажи.УстановитьОграниченияИспользованияВОтборе(ЗапрещенныеВОтбореПоля);
	
	ОтключитьНедоступныеОтборы(СписокДокументыПродажи.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтключитьНедоступныеОтборы(Коллекция, ДоступныеПоляОтбора = Неопределено)
	
	Для Каждого ТекущиеДанные Из Коллекция Цикл
		
		Если ТипЗнч(ТекущиеДанные) = Тип("ОтборКомпоновкиДанных") Тогда
			
			ОтключитьНедоступныеОтборы(ТекущиеДанные.Элементы, ТекущиеДанные.ДоступныеПоляОтбора);
			
		ИначеЕсли ТипЗнч(ТекущиеДанные) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			ОтключитьНедоступныеОтборы(ТекущиеДанные.Элементы, ДоступныеПоляОтбора);
			
		ИначеЕсли ТипЗнч(ТекущиеДанные) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			
			Если НЕ ТекущиеДанные.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			Для Каждого ИмяСвойства Из СтрРазделить("ЛевоеЗначение,ПравоеЗначение",",") Цикл
				
				ТекущееПоле = ТекущиеДанные[ИмяСвойства];
				Если ТипЗнч(ТекущееПоле) = Тип("ПолеКомпоновкиДанных") 
					И ДоступныеПоляОтбора.НайтиПоле(ТекущееПоле) = Неопределено Тогда
					
					ТекущиеДанные.Использование = Ложь;
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
```

Теперь когда пользователь очищает поле "Организация", то и наш "неправильный" отбор отключается.

![Скрин](17.png)

Можно заметить, что при этом поле остаётся. И самое интересное, что если пользователь вручную нажмёт его использование, то и, несмотря на ограничения отборов, фильтрация применится:

![Скрин](18.png)

Но что же делать? Придётся не отключать использование отбора, а просто удалять его.

Подправим код в расширении:

```bsl
&НаСервере
&После("УстановитьОтборПоОрганизации")
Процедура ЖурналДокументовПродажи_УстановитьОтборПоОрганизации()
	
	ЗапрещенныеВОтбореПоля = Новый Массив;
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		ЗапрещенныеВОтбореПоля.Добавить("Подразделение");
	КонецЕсли;
	
	СписокДокументыПродажи.УстановитьОграниченияИспользованияВОтборе(ЗапрещенныеВОтбореПоля);
	
	ОтключитьНедоступныеОтборы(СписокДокументыПродажи.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтключитьНедоступныеОтборы(Коллекция, ДоступныеПоляОтбора = Неопределено)
	
	УдаляемыеЭлементы = Новый Массив;
	
	Для Каждого ТекущиеДанные Из Коллекция Цикл
		
		Если ТипЗнч(ТекущиеДанные) = Тип("ОтборКомпоновкиДанных") Тогда
			
			ОтключитьНедоступныеОтборы(ТекущиеДанные.Элементы, ТекущиеДанные.ДоступныеПоляОтбора);
			
		ИначеЕсли ТипЗнч(ТекущиеДанные) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			ОтключитьНедоступныеОтборы(ТекущиеДанные.Элементы, ДоступныеПоляОтбора);
			
		ИначеЕсли ТипЗнч(ТекущиеДанные) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			
			Если НЕ ТекущиеДанные.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			Для Каждого ИмяСвойства Из СтрРазделить("ЛевоеЗначение,ПравоеЗначение",",") Цикл
				
				ТекущееПоле = ТекущиеДанные[ИмяСвойства];
				Если ТипЗнч(ТекущееПоле) = Тип("ПолеКомпоновкиДанных") 
					И ДоступныеПоляОтбора.НайтиПоле(ТекущееПоле) = Неопределено Тогда
					
					УдаляемыеЭлементы.Добавить(ТекущиеДанные);
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого УдаляемыйЭлемент Из УдаляемыеЭлементы Цикл
		Коллекция.Удалить(УдаляемыйЭлемент);
	КонецЦикла;
	
КонецПроцедуры
```

Теперь при очистке организации, так же удаляются и "запрещенные" отборы:

![Скрин](19.png)

Пользователь теперь не может наложить отбор, так как его нет в доступных полях для отбора. А старые отборы почистились автоматически.

Хорошо, мы разобрались с ограничениями отборов в динамическом списке. Но как обстоят дела с другими ограничениями? Добавим запреты на группировку и сортировку:

```bsl
&НаСервере
&После("УстановитьОтборПоОрганизации")
Процедура ЖурналДокументовПродажи_УстановитьОтборПоОрганизации()
	
	ЗапрещенныеПоля = Новый Массив;
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		ЗапрещенныеПоля.Добавить("Подразделение");
	КонецЕсли;
	
	СписокДокументыПродажи.УстановитьОграниченияИспользованияВОтборе(ЗапрещенныеПоля);
	СписокДокументыПродажи.УстановитьОграниченияИспользованияВГруппировке(ЗапрещенныеПоля);
	СписокДокументыПродажи.УстановитьОграниченияИспользованияВПорядке(ЗапрещенныеПоля);
	
	ОтключитьНедоступныеОтборы(СписокДокументыПродажи.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы);
	
КонецПроцедуры
```

Открываем форму с пустой организацией и видим, что подразделения нет в доступных к группировке полей. Всё хорошо. 

![Скрин](20.png)

Теперь выберем организацию и добавим группировку:

![Скрин](21.png)

Всё хорошо применилось:

![Скрин](22.png)

А теперь очистим организацию. После очистки, наш код запрещает использование поля Подразделение в группировках.

И вот мы видим такую ошибку:

![Скрин](23.png)

На группировку 1С поругалась. И форма теперь показывает пустой список:

То же самое и с ограничением по сортировке.

Выходит, что платформа позволяет блокировать такие действия. Но с отборами это не работает. Хорошо, что мы написали свой метод по обработке этого?

 

На первый взгляд, все эти проблемы возникнут только в тех случаях, если разработчик в рамках одного списка производит включение\отключение ограничений. Однако, это не совсем так.

Если изначально форма списка не содержала ограничения по отборам, то при внесении изменений, у пользователей могут остаться сохранённые настройки. Как и те, что подгружаются автоматически при открытии формы, так и те, что пользователь может подгрузить вручную по кнопке "Выбрать настройки".

И это так же нужно обрабатывать кодом. Иначе нет никакой уверенности, что ваши ограничения действительно заработали и пользователи не накладывают "запрещенные" отборы.

А напоследок, давайте вспомним один из хитрых методов обхода ограничений по отборам из статьи [Ограничения полей, или как обмануть СКД]({% link _posts/articles/2020-04-05-field-constraints-how-fool-skd.md %}).

Он позволяет наложить нехитрый отбор для примитивных типов полей, поэтому немного изменим наш код и ограничим поле "Сумма". И оставим пока только ограничение отборов. 
(В нашей обработке два поля "Сумма", поэтому, чтобы избежать путаницы, ограничим оба)

```bsl
&НаСервере
&После("УстановитьОтборПоОрганизации")
Процедура ЖурналДокументовПродажи_УстановитьОтборПоОрганизации()
	
	ЗапрещенныеПоля = Новый Массив;
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		ЗапрещенныеПоля.Добавить("Сумма");
		ЗапрещенныеПоля.Добавить("СуммаДокумента");
	КонецЕсли;
	
	СписокДокументыПродажи.УстановитьОграниченияИспользованияВОтборе(ЗапрещенныеПоля);
	
	ОтключитьНедоступныеОтборы(СписокДокументыПродажи.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы);
	
КонецПроцедуры
```

Для этого в форме с пустой организацией зайдём в "Настроить список". Как видим, поля суммы в отборах нет:

![Скрин](25.png)

А теперь нажмём "Ещё" -> "Изменить форму"

![Скрин](26.png)

И перенесем доступные к сортировке поля в группу с отборами:

![Скрин](27.png)

Вуаля!

![Скрин](28.png)

Организация у нас пустая, но поле Сумма теперь доступно. И мы можем сделать своё грязное дело - перенести поле в отбор:

![Скрин](29.png)

Фильтр применился. Несмотря на ограничения.

![Скрин](30.png)

Да, этот способ обхода замудрён, но работает. И к сожалению, события по изменению отборов в 1С нет...

Есть простой способ предотвратить это. Если вы отключаете использование в отборах, то и запрещайте использовать в группировках и сортировках. Тогда неоткуда будет подтянуть поле с отбором. 

**Выводы:**

Ограничения отборов в динамическом списке автоматически обрабатываются недостаточно, поэтому:

- Дополнительно программно обходите настройки (при установки ограничений и при загрузке пользовательских настроек)
- Если запрещаете отборы, то запрещайте и группировку\сортировку.
- Если планируете включать\отключать ограничения и у вас платформа меньше 8.3.16, то отключите отображения поля состояния просмотра, чтобы оно не вводило пользователя в заблуждение.

